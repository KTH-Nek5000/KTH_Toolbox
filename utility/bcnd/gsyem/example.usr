!=======================================================================
!> @brief Generate single eddy for a given family
!! @param[out] epos       eddy position
!! @param[out] eps        eddy orientation
!! @param[in]  nfam       family number
!! @param[in]  ifinit     intial distribution
      subroutine usr_gen_eddy(epos,eps,nfam,ifinit)
      implicit none

      include 'SIZE'
      include 'TSTEP'           ! pi
      include 'GSYEMD'

      ! argument list
      real epos(ldim)
      integer eps(ldim)
      integer nfam
      logical ifinit

      ! local variables
      real rho, theta, vrtmp(ldim)
      integer il

      real yp_cut
      parameter (yp_cut=0.45)
      
      ! functions
      real math_ran_rng
!-----------------------------------------------------------------------
      rho = yp_cut*sqrt(math_ran_rng(0.0,1.0))  
      theta = math_ran_rng(0.,2.0*pi)

      vrtmp(1) = rho*cos(theta)
      vrtmp(2) = rho*sin(theta)
      if (ifinit) then
         vrtmp(ldim) = math_ran_rng(-gsyem_bext(nfam),gsyem_bext(nfam))
      else
         vrtmp(ldim) = -gsyem_bext(nfam)
      endif

      ! rotate coordinates
      call math_rot3Da(epos,vrtmp,
     $        gsyem_raxs(1,nfam),gsyem_rang(nfam))

      do il=1,ldim
         rho = math_ran_rng(0.0,1.0)
         if (rho.gt.0.5) then
            eps(il) = 1
         else
            eps(il) = -1
         endif
      enddo

      return
      end subroutine
!=======================================================================
